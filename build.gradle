plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'io.github.slimjar' version '1.2.1'
    id "net.kyori.indra" version "2.0.5"
    id "net.kyori.indra.publishing.sonatype" version "2.0.5"
}

version "2.0"
description "Library for minecraft plugins"

ext {
    base = 'me.lorenzo0111.pluginslib.libraries'
}

indra {
    mitLicense()
    scm(scm -> scm
            .connection(String.format("scm:git:https://github.com/FoxPowered/PluginsLib.git"))
            .developerConnection(String.format("scm:git:ssh://git@github.com/FoxPowered/PluginsLib.git"))
            .url(String.format("https://github.com/FoxPowered/PluginsLib")))
    issues(issues ->
            issues.url("https://github.com/FoxPowered/PluginsLib/issues")
            .system("GitHub"))
}

indraSonatype {
    useAlternateSonatypeOSSHost("s01")
}

shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "io.github.slimjar"
    apply plugin: "maven-publish"

    group 'me.lorenzo0111'
    version '2.0'

    repositories {
        mavenCentral()
    }

    javadoc {
        options.links(
                'https://javadoc.io/static/org.jetbrains/annotations/20.1.0/',
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://papermc.io/javadocs/paper/1.16/'
        )
        source = sourceSets.main.allJava
        // destinationDir = new File("${System.getenv("javadocs")}/pluginslib/${version}")
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:21.0.1'
    }

    shadowJar {
        archiveFileName = "${project.name}-${project.version}.jar"
    }

    shadowJar {
        relocate("org.yaml.snakeyaml","${base}.yaml")
        relocate("org.spongepowered.configurate","${base}.configurate")
        relocate("org.checkerframework","${base}.checkerframework")
        relocate("io.leangen.geantyref","${base}.geantyref")
        relocate("com.google","${base}")
    }

    tasks.build.dependsOn tasks.shadowJar
}

tasks.build.dependsOn tasks.shadowJar
